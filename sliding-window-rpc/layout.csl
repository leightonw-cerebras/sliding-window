param kernel_width: u16;
param num_elems: u16;

const last_pe = kernel_width-1;
const middle_pe = last_pe/2;

// Instantiate memcpy infrastructure
const memcpy = @import_module("<memcpy_multi/get_params>", .{
  .width = kernel_width,
  .height = 1
  });

const color0 = @get_color(2);
const color1 = @get_color(3);
const color2 = @get_color(4);
const color3 = @get_color(5);

layout {
  // Use a single row of kernel_width PEs (columns=kernel_width, rows=1)
  @set_rectangle(kernel_width, 1);

  // LEFT EDGE
  ////////////
  @set_tile_code(0, 0, "pe_edge.csl", .{
    .memcpy_params = memcpy.get_params(0), .num_elems = num_elems,
    .fab_send_color = color0, .fab_recv_color = color1 });

  @set_color_config(0, 0, color0, .{ .routes = .{ .rx = .{ RAMP }, .tx = .{ EAST } }});
  @set_color_config(0, 0, color1, .{ .routes = .{ .rx = .{ EAST }, .tx = .{ RAMP } }});

  // RIGHT EDGE
  /////////////
  @set_tile_code(last_pe, 0, "pe_edge.csl", .{
    .memcpy_params = memcpy.get_params(last_pe), .num_elems = num_elems,
    .fab_send_color = color2, .fab_recv_color = color3 });

  @set_color_config(last_pe, 0, color2, .{ .routes = .{ .rx = .{ RAMP }, .tx = .{ WEST } }});
  @set_color_config(last_pe, 0, color3, .{ .routes = .{ .rx = .{ WEST }, .tx = .{ RAMP } }});

  // MIDDLE
  /////////
  @set_tile_code(middle_pe, 0, "pe_middle.csl", .{
    .num_elems = num_elems,
    .recv_left_color = color0, .send_left_color = color1,
    .recv_right_color = color2, .send_right_color = color3,
    .memcpy_params = memcpy.get_params(middle_pe) });

  @set_color_config(middle_pe, 0, color0, .{ .routes = .{ .rx = .{ WEST }, .tx = .{ RAMP } }});
  @set_color_config(middle_pe, 0, color1, .{ .routes = .{ .rx = .{ RAMP }, .tx = .{ WEST } }});
  @set_color_config(middle_pe, 0, color2, .{ .routes = .{ .rx = .{ EAST }, .tx = .{ RAMP } }});
  @set_color_config(middle_pe, 0, color3, .{ .routes = .{ .rx = .{ RAMP }, .tx = .{ EAST } }});

  // BETWEEN LEFT AND MIDDLE
  //////////////////////////
  var idx: u16 = 1;
  while (idx < middle_pe) : (idx += 1) {
    @set_tile_code(idx, 0, "pe_noop.csl", .{
      .num_elems = num_elems,
      .memcpy_params = memcpy.get_params(idx) });

    @set_color_config(idx, 0, color0, .{ .routes = .{ .rx = .{ WEST }, .tx = .{ EAST } }});
    @set_color_config(idx, 0, color1, .{ .routes = .{ .rx = .{ EAST }, .tx = .{ WEST } }});
  }

  // BETWEEN MIDDLE AND RIGHT
  ///////////////////////////
  idx = middle_pe + 1;
  while (idx < last_pe) : (idx += 1) {
    @set_tile_code(idx, 0, "pe_noop.csl", .{
      .num_elems = num_elems,
      .memcpy_params = memcpy.get_params(idx) });

    @set_color_config(idx, 0, color2, .{ .routes = .{ .rx = .{ EAST }, .tx = .{ WEST } }});
    @set_color_config(idx, 0, color3, .{ .routes = .{ .rx = .{ WEST }, .tx = .{ EAST } }});
  }

  @export_name("arr0", [*]f32, true);
  @export_name("arr1", [*]f32, true);
  @export_name("maxmin_time", [*]f32, true);
  @export_name("main_fn", fn()void);

    // PE: 0 --- 1 --- 2 --- 3 --- 4 --- 5 --- 6 --- 7 --- 8

    // 0:  >     >     >     >     >
    //     ^                       v

    // 1:  <     <     <     <     <
    //     v                       ^

    // 2:                          <     <     <     <     <
    //                             v                       ^

    // 3:                          >     >     >     >     >
    //                             ^                       v
}
